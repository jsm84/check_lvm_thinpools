#!/usr/bin/perl
use strict;
use warnings;

use Getopt::Std;

getopt('wc');
our($opt_w, $opt_c);

my $warn = $opt_w || 75.0;
my $crit = $opt_c || 90.0;

# strip the percent sign from argument input
$warn =~ s#%##x;
$crit =~ s#%##x;

if ($warn < 0 || $warn > 100.0) {
  $warn = 75.0;
} elsif ($warn < 50.0) {
  $warn = 100.0 - $warn; #existing nagio0s check_disk plugin reports disk% free while lvs reports disk% used.
}

if ($crit < 0 || $crit > 100.0) {
  $crit = 90.0;
} elsif ($crit < $warn) { #if critical value is less than warn value, then the arg is referring to %free
  $crit = 100.0 - $crit;
}

sub get_thinpools {
  my @lvsoutput = `lvs --noheadings --separator : -o lv_attr,lv_name,data_percent,metadata_percent`;
  my @thinpools;

  for my $lvsline (@lvsoutput) {
    if ($lvsline =~ m#^(?:\s+)?(.*):(.*):(.*):(.*)#x) {
      my $lv = {
        'lv_attr'          => $1,
        'lv_name'          => $2,
        'data_percent'     => $3,
        'metadata_percent' => $4,
      };

      if ($lv->{lv_attr} =~ m#^t#x) {
        push @thinpools, $lv;
      }
    }
  }

  return @thinpools;
}

sub check_thinpools {
  for my $thinpool (get_thinpools()) {
    # Check metadata usage
    if ($thinpool->{metadata_percent} > $crit) {
      add_error(2, "LVM Thinpool CRITICAL: $thinpool->{lv_name} Meta Usage is $thinpool->{metadata_percent}%")
    } elsif ($thinpool->{metadata_percent} > $warn) {
      add_error(1, "LVM Thinpool WARNING: $thinpool->{lv_name} Meta Usage is $thinpool->{metadata_percent}%")
    }

    # Check data usage
    if ($thinpool->{data_percent} > $crit) {
      add_error(2, "LVM Thinpool CRITICAL: $thinpool->{lv_name} Data Usage is $thinpool->{data_percent}%")
    } elsif ($thinpool->{data_percent} > $warn) {
      add_error(1, "LVM Thinpool WARNING: $thinpool->{lv_name} Data Usage is $thinpool->{data_percent}%")
    }
  }

  return;
}

my @errors;

sub add_error {
  my ($exit_code, $message) = @_;

  push @errors, {
    'exit_code' => $exit_code,
    'message'   => $message,
  };

  return;
}

sub aggregate_errors {
  # Sort errors, highest exit code first
  my @sorted_errors = sort { $b->{exit_code} cmp $a->{exit_code} } @errors;
  if (scalar @sorted_errors != 0) {
    for my $error (@sorted_errors) {
      print $error->{message}, "\n";
    }
    exit $sorted_errors[0]->{exit_code};
  }

  return;
}

my @thinpools = get_thinpools();

# No thinpool found?
if (scalar @thinpools == 0) {
  print "UNKNOWN: No LVM Thinpools found.\n";
  exit 3;
} else {
  check_thinpools();
  aggregate_errors();
}

for my $thinpool (@thinpools) {
  print "LVM Thinpool OK - $thinpool->{lv_name} Data Usage is $thinpool->{data_percent}%. Meta Usage is $thinpool->{metadata_percent}%\n";
}
exit 0;
